plugins {
    id "org.wisepersist.gwt" version "1.1.14"
}
apply plugin: "java"
apply plugin: "maven-publish"
if(project.gradle.startParameter.taskNames.contains("gwtDev")) {
    apply plugin: "war"
    apply plugin: "gwt"
    gwt.modules "net.sayaya.Test"
    sourceSets.main.java.srcDirs += 'src/test/java'
} else {
    apply plugin: "gwt-base"
}

group "net.sayaya"
version "1.0"
[compileJava, compileTestJava]*.options*.encoding = "UTF-8"

repositories {
    mavenLocal()
    mavenCentral()
}
dependencies {
    implementation("net.sayaya:ui:3.1")
    implementation("org.jboss.elemento:elemento-core:1.0.3")
    implementation("com.google.elemental2:elemental2-svg:1.1.0")
    implementation("com.google.gwt:gwt-user:2.9.0")
    implementation("com.google.gwt:gwt-dev:2.9.0")
    implementation("org.projectlombok:lombok:1.18.20")
    annotationProcessor("org.projectlombok:lombok:1.18.20")
}
gwt {
    gwtVersion = "2.9.0"
    minHeapSize = "1024M"
    maxHeapSize = "2048M"
    jsInteropExports.setGenerate(true)
    compiler {
        localWorkers = 12
        disableClassMetadata = true
        disableCastChecking = true
    }
}
ext {
    lombok = project.configurations.annotationProcessor.find { it.name.startsWith("lombok") }
}
if(project.gradle.startParameter.taskNames.contains("gwtDev")) {
    gwtDev {
        jvmArgs "-XX:ReservedCodeCacheSize=512M","-javaagent:${lombok}=ECJ"
        port 8888
        war file("src/test/webapp")
    }
} else {
    jar {
        from sourceSets.main.allSource
        archiveFileName = "sayaya-${archivesBaseName}.jar"
    }
}
compileGwt {
    minHeapSize = "2048M"
    maxHeapSize = "4096M"
    jvmArgs "-XX:ReservedCodeCacheSize=512M","-Xss2M","-javaagent:${lombok}=ECJ","-generateJsInteropExport"
}
publishing {
    publications {
        maven(MavenPublication) {
            groupId = "net.sayaya"
            artifactId = "chart"
            version = "1.0"
            from components.java
        }
    }
}